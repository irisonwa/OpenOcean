#version 460 core

layout (local_size_x = 32) in;

// the maximum amount of keyframes allowed. 
// requires that skinned meshes be exported without sampling or that animation has less than 32 frames total
#define MAX_KEYFRAMES 32
#define MAX_JOINTS 16

// 64 + 64 + 512 + 512 + 512 + 128 + 16 = 1808 bytes
struct Animation {
    mat4 relTransformation;                  // transformation relative to parent (aiNode->mTransformation)          # 4 * 4 * 4 = 64
    mat4 globalInvTransform;                 // global inverse transform of root node of mesh's skeleton             # 4 * 4 * 4 = 64
    vec4 positionKeys[MAX_KEYFRAMES];        // changes in position (first 3) and time of keys (last float)          # 4 * 4 * 32 = 512
    vec4 scalingKeys[MAX_KEYFRAMES];         // changes in scaling (first 3) and time of keys (last float)           # 4 * 4 * 32 = 512
    vec4 rotationKeys[MAX_KEYFRAMES];        // changes in rotation                                                  # 4 * 4 * 32 = 512
    float rotationKeysTimes[MAX_KEYFRAMES];  // times of rotation keys                                               # 4 * 32 = 128
    int boneIndex;                           // index of bone this animation applies to                              # 4
    float animDuration;                      // length of the animation in ticks                                     # 4
    // vec2 an_padding;                      // padding                                                              # 4 * 2 = 8
};

// 64 + 64 + 64 + 4 + 12 = 208 bytes
struct BoneInfo {
    mat4 offsetMatrix;                       // inverse bind matrix for the bone                                     # 4 * 4 * 4 = 64
    mat4 currentTransformation;              // current transformation of the bone                                   # 4 * 4 * 4 = 64
    int children[MAX_JOINTS];                // ids of children bones                                                # 4 * 16 = 64
    int ID;                                  // bone id                                                              # 4
    // vec2 bi_padding;                      // padding                                                              # 4 * 2 = 8
    // float bi_padding2;                    // padding                                                              # 4
};

layout (std430, binding = 0) buffer Animations {
    Animation anims[];
};

layout (std430, binding = 1) buffer BoneInfos {
    BoneInfo infos[];
};

layout (std430, binding = 2) buffer BoneOffsets {
    int boffsets[];
};

uniform float timeSinceApplicationStarted;

vec4 slerp(vec4 q1, vec4 q2, float t);
mat3 quatToMat(vec4 q);
mat4 getScaleMat(vec3 scale);
mat4 getTranslationMat(vec3 pos);
mat4 applyTransformation(vec3 pos, vec4 rot, vec3 scale);
vec3 interpolateTrans(int animIdx, float atime);
vec3 interpolateScale(int animIdx, float atime);
vec4 interpolateRotation(int animIdx, float atime);

void main() {
    int idStack[100];
    mat4 parentStack[100];
    for (int i = 0; i < boffsets.length() - 1; i++) { // offsets length is #variants + 1
        float wrappedTime = mod(timeSinceApplicationStarted * 24 * 20, anims[boffsets[i]].animDuration);
        int idP = 0;
        int paP = 0;
        idStack[idP++] = 0;
        parentStack[paP++] = anims[boffsets[i]].relTransformation;
        while (idP > 0) {
            int ai = idStack[--idP] + boffsets[i];
            int bi = anims[ai].boneIndex + boffsets[i];
            mat4 parent = parentStack[--paP];

            vec3 transVec = interpolateTrans(ai, wrappedTime);
            vec4 rotQuat = interpolateRotation(ai, wrappedTime);
            vec3 scaleVec = interpolateScale(ai, wrappedTime);
            
            mat4 boneTrans = applyTransformation(transVec, rotQuat, scaleVec);
            mat4 globalTrans = parent * boneTrans;
            infos[bi].currentTransformation = 
                anims[ai].globalInvTransform * globalTrans * infos[bi].offsetMatrix;

            for (int j = MAX_JOINTS - 1; j >= 0; j--) {
                if (infos[bi].children[j] != -1) {
                    idStack[idP++] = infos[bi].children[j];
                    parentStack[paP++] = globalTrans;
                }
            }
        }
    }
}

// interpolate quaternions
// copied from assimp's implementation, which itself was adopted from gmtl
vec4 slerp(vec4 q1, vec4 q2, float t) {
    float epsilon = 0.00001; // min float distance to be considered equal

    float cos_omega = 
        q1.x * q2.x + 
        q1.y * q2.y + 
        q1.z * q2.z + 
        q1.w * q2.w;
    
    vec4 end = q2;
    // reverse signs if quaternion is negative
    if (cos_omega < 0.0) {
        cos_omega = -cos_omega;
        end.x = -end.x;
        end.y = -end.y;
        end.z = -end.z;
        end.w = -end.w;
    }

    float factor_start = 0;
    float factor_end = 0;
    if (1 - cos_omega > epsilon) {
        float omega = acos(cos_omega);
        float sin_omega = sin(omega);
        factor_start = sin(omega * (1 - t)) / sin_omega;
        factor_end = sin(t * omega) / sin_omega;
    } else {
        // do linear interpolation if near the end of the animation
        factor_start = 1 - t;
        factor_end = t;
    }

    vec4 outq = vec4(
        factor_start * q1.x + factor_end * end.x,
        factor_start * q1.y + factor_end * end.y,
        factor_start * q1.z + factor_end * end.z,
        factor_start * q1.w + factor_end * end.w
    );
    return outq;
}

// quaternion to mat3
// taken from assimp's implementation but transposed since assimp uses a row-major format
mat3 quatToMat(vec4 q) {
    mat3 m = mat3(1);
    float x = q.x;
    float y = q.y;
    float z = q.z;
    float w = q.w;

    m[0][0] = 1 - 2 * (y*y + z*z);
    m[0][1] = 2 * (x*y + z*w);
    m[0][2] = 2 * (x*z - w*y);
    m[1][0] = 2 * (x*y - z*w);
    m[1][1] = 1 - 2 * (x*x + z*z);
    m[1][2] = 2 * (y*z + x*w);
    m[2][0] = 2 * (x*z + y*w);
    m[2][1] = 2 * (y*z - x*w);
    m[2][2] = 1 - 2 * (x*x + y*y);
    
    return m;
}

mat4 getScaleMat(vec3 scale) {
    mat4 m = mat4(1);
    m[0] *= scale[0];
    m[1] *= scale[1];
    m[2] *= scale[2];
    return m;
}

mat4 getTranslationMat(vec3 pos) {
    mat4 m = mat4(1);
    m[3][0] = pos.x;
    m[3][1] = pos.y;
    m[3][2] = pos.z;
    return m;
}

mat4 applyTransformation(vec3 pos, vec4 rot, vec3 scale) {
    mat4 transMat = getTranslationMat(pos);
    mat4 rotMat = mat4(quatToMat(rot));
    mat4 scaleMat = getScaleMat(scale);
    return transMat * rotMat * scaleMat;
}

vec3 interpolateTrans(int animIdx, float atime) {
    if (anims[animIdx].positionKeys[1].x == -1) {
        return anims[animIdx].positionKeys[0].xyz;
    }

    // get position index
    uint positionIndex = 0;
    for (uint i = 0; i < MAX_KEYFRAMES - 1; i++) {
        float t = anims[animIdx].positionKeys[i + 1].w;  // time of key
        if (atime < t) {
            positionIndex = i;
            break;
        }
    }

    float deltaTime = anims[animIdx].positionKeys[positionIndex + 1].w - anims[animIdx].positionKeys[positionIndex].w; 
    float factor = (atime - anims[animIdx].positionKeys[positionIndex].w) / deltaTime;
    // return mix(start, end, factor);

    vec3 start = vec3(anims[animIdx].positionKeys[positionIndex]);    // start position value
    vec3 end = vec3(anims[animIdx].positionKeys[positionIndex + 1]);  // end position value
    return start + factor * (end - start);                   // interpolated position
}

vec3 interpolateScale(int animIdx, float atime) {
    if (anims[animIdx].scalingKeys[1].x == -1) {
        return anims[animIdx].scalingKeys[0].xyz;
    }

    // get position index
    uint positionIndex = 0;
    for (uint i = 0; i < MAX_KEYFRAMES - 1; i++) {
        float t = anims[animIdx].scalingKeys[i + 1].w;  // time of key
        if (atime < t) {
            positionIndex = i;
            break;
        }
    }

    float deltaTime = anims[animIdx].scalingKeys[positionIndex + 1].w - anims[animIdx].scalingKeys[positionIndex].w;
    float factor = (atime - anims[animIdx].scalingKeys[positionIndex].w) / deltaTime;
    // return mix(start, end, factor);

    vec3 start = vec3(anims[animIdx].scalingKeys[positionIndex]);    // start position value
    vec3 end = vec3(anims[animIdx].scalingKeys[positionIndex + 1]);  // end position value
    return start + factor * (end - start);                   // interpolated position
}


vec4 interpolateRotation(int animIdx, float atime) {
    if (anims[animIdx].rotationKeys[1].x == -1) {
        return anims[animIdx].rotationKeys[0];
    }

    // get scale index
    uint rotationIndex = 0;
    for (uint i = 0; i < MAX_KEYFRAMES - 1; i++) {
        float t = anims[animIdx].rotationKeysTimes[i + 1];
        if (atime < t) {
            rotationIndex = i;
            break;
        }
    }

    float deltaTime = anims[animIdx].rotationKeysTimes[rotationIndex + 1] - anims[animIdx].rotationKeysTimes[rotationIndex];
    float factor = (atime - anims[animIdx].rotationKeysTimes[rotationIndex]) / deltaTime;

    vec4 start = anims[animIdx].rotationKeys[rotationIndex];    // start rotation value
    vec4 end = anims[animIdx].rotationKeys[rotationIndex + 1];  // end rotation value
    return normalize(slerp(start, end, factor));       // interpolated rotation
}

